
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КоличествоПериодов = 30;
	НачалоПериода = НачалоГода(ТекущаяДата());
	НомерТекущейВыделеннойКолонки = 1;
	
	СоздатьРеквизитыИЭлементыФормы();
	
	ЗаполнитьТаблицыДанными();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьРеквизитыИЭлементыФормы()
	
	Форма = ЭтотОбъект;
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для НомерТаблицы = 1 По 3 Цикл
		Для НомерПериода = 1 По КоличествоПериодов Цикл
			РОК_МодификацияФорм.СоздатьДобавитьРеквизитФормы(Форма,
				ДобавляемыеРеквизиты,
				"Период"+НомерПериода,
				"Число",
				"Таблица"+НомерТаблицы);
		КонецЦикла;
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для НомерТаблицы = 1 По 3 Цикл
		Для НомерПериода = 1 По КоличествоПериодов Цикл
			РОК_МодификацияФорм.СоздатьПолеФормы(
				Форма.Элементы,
				"Таблица"+НомерТаблицы+"Период"+НомерПериода,
				"Таблица"+НомерТаблицы+".Период"+НомерПериода,
				Формат(ДобавитьМесяц(НачалоПериода, НомерПериода -1), "ДФ='MMMM yyyy'"),
				ВидПоляФормы.ПолеВвода,
				Форма.Элементы["Таблица"+НомерТаблицы]);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыДанными()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 700
	|	Номенклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	Пока Выборка.Следующий() Цикл
		Для Инд = 1 По 3 Цикл
			СтрокаТаблицы = ЭтотОбъект["Таблица"+Инд].Добавить();
			
			СтрокаТаблицы.Номенклатура = Выборка.Номенклатура;
			
			Для НомерПериода = 1 По КоличествоПериодов Цикл
				СтрокаТаблицы["Период"+НомерПериода] = ГСЧ.СлучайноеЧисло(0, 1000);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Таблица1ПриАктивизацииЯчейки(Элемент)	
	
	ИмяКолонки = Элементы.Таблица1.ТекущийЭлемент.Имя;
	
	Если СтрНайти(ИмяКолонки, "Период") > 0 Тогда
		Номенклатура = Элементы.Таблица1.ТекущиеДанные.Номенклатура;
		НомерНовогоВыделенногоПериода = Число(СтрЗаменить(ИмяКолонки, "Таблица1Период", ""));
		
		Отбор = Новый Структура("Номенклатура", Номенклатура);		
		
		// перенос фокуса на таблицу 2
		НайденныеСтроки = Таблица2.НайтиСтроки(Отбор);
		ИдентификаторСтроки = НайденныеСтроки[0].ПолучитьИдентификатор();
		
		ТекущийЭлемент = Элементы["Таблица2Период"+НомерНовогоВыделенногоПериода];
		ТекущийЭлемент.ТекущаяСтрока = ИдентификаторСтроки;
		
		// перенос фокуса на таблицу 3
		НайденныеСтроки = Таблица3.НайтиСтроки(Отбор);
		ИдентификаторСтроки = НайденныеСтроки[0].ПолучитьИдентификатор();
		
		ТекущийЭлемент = Элементы["Таблица3Период"+НомерНовогоВыделенногоПериода];
		ТекущийЭлемент.ТекущаяСтрока = ИдентификаторСтроки;
		
		
		// возврат фокуса на таблицу 1
		ТекущийЭлемент = Элементы["Таблица1Период"+НомерНовогоВыделенногоПериода];
		
		Для НомерТаблицы = 1 По 3 Цикл
			Элементы["Таблица"+НомерТаблицы+"Период"+НомерТекущейВыделеннойКолонки].ЦветФона = Новый Цвет();
			Элементы["Таблица"+НомерТаблицы+"Период"+НомерНовогоВыделенногоПериода].ЦветФона = Новый Цвет (254, 242, 199);
			//Элементы["Таблица"+НомерТаблицы].Обновить();
		КонецЦикла;
		
		НомерТекущейВыделеннойКолонки = НомерНовогоВыделенногоПериода;
	КонецЕсли;
	
КонецПроцедуры
